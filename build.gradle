import org.gradle.api.attributes.java.TargetJvmEnvironment

/**
 * 1) Update agpVersions with range of desired versions
 * 2) Run './gradlew dumpSources'
 * 3) Check changeset into source control
 */

// https://mvnrepository.com/artifact/com.android.tools.build/gradle
def agpVersions = [
  "8.13.1",
  "9.0.0-beta02"
]

repositories {
  google()
  mavenCentral()
}

// anchor task
def dumpSources = tasks.register("dumpSources")

agpVersions.forEach { agpVersion ->
  // create configuration for specific version of AGP
  def agpConfiguration = configurations.create("agp${agpVersion}") {
    // TODO: https://github.com/google/guava/issues/6801
    //  Fix `Cannot choose between the following variants of com.google.guava:guava:33.3.1-jre: androidRuntimeElements, jreRuntimeElements`.
    attributes.attribute(
      TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
      objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM),
    )
  }

  // add that version of AGP as a dependency to this configuration
  agpConfiguration.dependencies.add(
    dependencies.create("com.android.tools.build:gradle:${agpVersion}")
  )

  // create a task dedicated to extracting sources for that version
  def agpDumpSources = tasks.register("dump${agpVersion}Sources", Copy) {
    inputs.files agpConfiguration
    into(layout.projectDirectory.dir(agpVersion))

    def componentIds = agpConfiguration
      .incoming
      .resolutionResult
      .allDependencies
      .findAll { it.selected.id.group.startsWith('com.android.tools') }
      .collect { it.selected.id }
      .toSet()

    dependencies.createArtifactResolutionQuery()
      .forComponents(componentIds)
      .withArtifacts(JvmLibrary, SourcesArtifact)
      .execute()
      .resolvedComponents
      .collectMany { it.getArtifacts(SourcesArtifact) }
      .findAll { it instanceof ResolvedArtifactResult }
      .each { ResolvedArtifactResult rar ->
        logger.lifecycle("Found sources jar: ${rar.file}")
        ModuleComponentIdentifier id = rar.id.componentIdentifier

        def group = id.group
        def module = id.module
        logger.lifecycle("Extracting to path: $agpVersion/$group/$module")
        from(zipTree(rar.file)) {
          into("$group/$module")
        }
      }
  }

  // hook anchor task to all version-specific tasks
  dumpSources.configure { it.dependsOn(agpDumpSources) }
}
