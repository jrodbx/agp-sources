import org.gradle.api.attributes.java.TargetJvmEnvironment

/**
 * 1) Update agpVersions with range of desired versions
 * 2) Run './gradlew dumpSources'
 * 3) Check changeset into source control
 */

// https://mvnrepository.com/artifact/com.android.tools.build/gradle
def agpVersions = [
  "8.9.3",
  "8.10.0-rc03",
  "8.10.0-rc04",
  "8.10.0",
  "8.10.1",
  "8.11.0-alpha07",
  "8.11.0-alpha08",
  "8.11.0-alpha09",
  "8.11.0-alpha10",
  "8.11.0-rc01",
  "8.11.0-rc02",
  "8.11.0",
  "8.11.1",
  "8.11.2",
  "8.12.0-alpha01",
  "8.12.0-alpha02",
  "8.12.0-alpha03",
  "8.12.0-alpha04",
  "8.12.0-alpha05",
  "8.12.0-alpha06",
  "8.12.0-alpha07",
  "8.12.0-alpha08",
  "8.12.0-alpha09",
  "8.12.0-rc01",
  "8.12.0",
  "8.12.1",
  "8.12.2",
  "8.12.3",
  "8.13.0-alpha01",
  "8.13.0-alpha02",
  "8.13.0-alpha03",
  "8.13.0-alpha04",
  "8.13.0-rc01",
  "8.13.0-rc02",
  "8.13.0",
  "8.13.1",
  "9.0.0-alpha01",
  "9.0.0-alpha02",
  "9.0.0-alpha03",
  "9.0.0-alpha04",
  "9.0.0-alpha05",
  "9.0.0-alpha06",
  "9.0.0-alpha07",
  "9.0.0-alpha08",
  "9.0.0-alpha09",
  "9.0.0-alpha10",
  "9.0.0-alpha11",
  "9.0.0-alpha12",
  "9.0.0-alpha13",
  "9.0.0-alpha14",
  "9.0.0-beta01",
  "9.0.0-beta02",
]

repositories {
  google()
  jcenter()
}

// anchor task
def dumpSources = tasks.register("dumpSources")

agpVersions.forEach { agpVersion ->
  // create configuration for specific version of AGP
  def agpConfiguration = configurations.create("agp${agpVersion}") {
    // TODO: https://github.com/google/guava/issues/6801
    //  Fix `Cannot choose between the following variants of com.google.guava:guava:33.3.1-jre: androidRuntimeElements, jreRuntimeElements`.
    attributes.attribute(
      TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
      objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM),
    )
  }
  configurations.add(agpConfiguration)

  // add that version of AGP as a dependency to this configuration
  agpConfiguration.dependencies.add(
    dependencies.create("com.android.tools.build:gradle:${agpVersion}")
  )

  // create a task dedicated to extracting sources for that version
  def agpDumpSources = tasks.register("dump${agpVersion}Sources") {
    inputs.files configurations.getByName("agp${agpVersion}")
    outputs.dir "$agpVersion/"

    doLast {
      def componentIds = configurations.getByName("agp${agpVersion}")
			    .incoming
				  .resolutionResult
				  .allDependencies
          .findAll { it.selected.id.group.startsWith('com.android.tools') }
          .collect { it.selected.id }
          .toSet()
      ArtifactResolutionResult result = dependencies.createArtifactResolutionQuery()
          .forComponents(componentIds)
          .withArtifacts(JvmLibrary, SourcesArtifact)
          .execute()
      result.resolvedComponents.each { ComponentArtifactsResult component ->
        Set<ArtifactResult> sources = component.getArtifacts(SourcesArtifact)
        sources.each { ArtifactResult ar ->
          println "Found ${ar.file}."
          if (ar instanceof ResolvedArtifactResult) {
            def group = ar.id.componentIdentifier.group
            def module = ar.id.componentIdentifier.module
            def version = ar.id.componentIdentifier.version
            println "Extracting to $agpVersion/$group/$module."
            copy {
              from zipTree(ar.file)
              into file("$agpVersion/$group/$module")
            }
            println "Done extracting $module."
          }
        }
      }
    }
  }

  // hook anchor task to all version-specific tasks
  dumpSources.configure { it.dependsOn(agpDumpSources) }
}
